#Использовать 1commands
#Использовать fs

Перем СтрокаЗапускаБСЛЛС;

&Пластилин
Перем НастройкиПроекта;

&Пластилин
Перем ТипОтчетаБСЛЛС;

&Пластилин
Перем ИмяФайлаМетаданныхОтчетаБСЛЛС;

&Пластилин
Перем ПодкаталогОтчетовБСЛЛС;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин НастройкиРабочейОбласти)
	СтрокаЗапускаБСЛЛС = НастройкиРабочейОбласти.СтрокаЗапускаБСЛЛС;

КонецПроцедуры

Функция ВыполнитьАнализ(КаталогИсходников, Цель = "") Экспорт

	ДатаАнализа = ТекущаяДата();
	Идентификатор = Строка(Новый УникальныйИдентификатор);

	Каталог = ОбъединитьПути(НастройкиПроекта.КаталогСборки(), ПодкаталогОтчетовБСЛЛС, КаталогТекущейСборки(ДатаАнализа, Идентификатор));

	ФС.ОбеспечитьКаталог(Каталог);

	Команда = Новый Команда;
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапускаБСЛЛС);
	Команда.ДобавитьПараметр("analyze");
	Команда.ДобавитьПараметр("--srcDir=" + КаталогИсходников);
	Команда.ДобавитьПараметр("--outputDir=" + Каталог);
	Команда.ДобавитьПараметр("--reporter=" + ТипОтчетаБСЛЛС);

	Команда.Исполнить();

	ЗаписатьМетаданныеОтчета(Каталог, 
								Новый Структура("Дата, КаталогИсходников, Цель, ТипОтчета, Идентификатор",
								ДатаАнализа, КаталогИсходников, Цель, ТипОтчетаБСЛЛС, Идентификатор));

	Возврат Команда.ПолучитьВывод();

КонецФункции

Функция АнализКонфигурации() Экспорт

	КаталогИсходников = НастройкиПроекта.КаталогКонфигурации();
	Возврат ВыполнитьАнализ(КаталогИсходников, "Конфигурация");
	
КонецФункции

Функция АналиРасширения(ИмяРасширения) Экспорт

	Каталог = НастройкиПроекта.КаталогРасширенияПоИмени(ИмяРасширения);

	Если НЕ ЗначениеЗаполнено(Каталог) Тогда
		ВызватьИсключение "Не указан каталог расширения " + ИмяРасширения;
	КонецЕсли;

	Возврат ВыполнитьАнализ(Каталог, ИмяРасширения);

КонецФункции

Процедура ЗаписатьМетаданныеОтчета(Каталог, Метаданные)
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл(ОбъединитьПути(Каталог, ИмяФайлаМетаданныхОтчетаБСЛЛС));
	ЗаписатьJSON(ЗаписьJSON, Метаданные);
	ЗаписьJSON.Закрыть();
КонецПроцедуры

Функция КаталогТекущейСборки(ДатаАнализа, Идентификатор)
	Возврат Формат(ДатаАнализа, "ДФ=yyyy-MM-dd-HH-mm-ss") + "-" + Идентификатор;
КонецФункции