
&Пластилин Перем УведомленияПользователям;
&Пластилин Перем ОчередьЗаданийПользователя;

&Пластилин
Перем АдаптерВанессаАДД;

&Контроллер("/vanessaadd")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ТочкаМаршрута("cmd/runAllTest")
Процедура ВыполнитьТесты() Экспорт
	ОчередьЗаданийПользователя.Добавить("Запуск тестов Vanessa-ADD", 
		Новый Действие(ЭтотОбъект, "ВыполнитьТестыВспомогательный")
	);
КонецПроцедуры

&ТочкаМаршрута("cmd/runTest/{ИмяОбработки}")
Процедура ВыполнитьТест(ИмяОбработки) Экспорт
	ОчередьЗаданийПользователя.Добавить("Запуск теста Vanessa-ADD: " + ИмяОбработки, 
		Новый Действие(ЭтотОбъект, "ВыполнитьТестВспомогательный"), 
		ИмяОбработки
	);
КонецПроцедуры

&ТочкаМаршрута("")
&Отображение("./src/Классы/interface/view/VanessaADD.html")
Процедура Главная(Ответ) Экспорт

	Ответ.Модель = Новый Структура("ТаблицаОбработок", АдаптерВанессаАДД.ТаблицаОбработок());

КонецПроцедуры

&ТочкаМаршрута("cmd/compileall")
Процедура СобратьВсе() Экспорт
	ОчередьЗаданийПользователя.Добавить("Сборка тестов Vanessa-ADD", 
		Новый Действие(ЭтотОбъект, "СобратьВсеВспомогательный")
	);
КонецПроцедуры

&ТочкаМаршрута("cmd/decompileall")
Процедура РазобратьВсе() Экспорт
	ОчередьЗаданийПользователя.Добавить("Разбор тестов Vanessa-ADD", 
		Новый Действие(ЭтотОбъект, "РазобратьВсеВспомогательный")
	);
КонецПроцедуры

&ТочкаМаршрута("cmd/compile/{ИмяОбработки}")
Процедура СобратьОдну(ИмяОбработки) Экспорт
	ОчередьЗаданийПользователя.Добавить("Сборка теста Vanessa-ADD: " + ИмяОбработки, 
		Новый Действие(ЭтотОбъект, "СобратьОднуВспомогательный"), 
		ИмяОбработки
	);
КонецПроцедуры

&ТочкаМаршрута("cmd/decompile/{ИмяОбработки}")
Процедура РазобратьОдну(ИмяОбработки) Экспорт
	ОчередьЗаданийПользователя.Добавить("Разбор теста Vanessa-ADD: " +  ИмяОбработки, 
		Новый Действие(ЭтотОбъект, "РазобратьОднуВспомогательный"), 
		ИмяОбработки
	);
КонецПроцедуры

#Область Вспомогательные

Процедура ВыполнитьТестыВспомогательный() Экспорт
	Результат = АдаптерВанессаАДД.ВыполнитьТесты();
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры

Процедура СобратьВсеВспомогательный() Экспорт
	Результат = АдаптерВанессаАДД.СобратьВсе();
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры

Процедура РазобратьВсеВспомогательный() Экспорт
	Результат = АдаптерВанессаАДД.РазобратьВсе();
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры


Процедура СобратьОднуВспомогательный(ИмяОбработки) Экспорт
	Результат = АдаптерВанессаАДД.Собрать(ИмяОбработки);
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры

Процедура РазобратьОднуВспомогательный(ИмяОбработки) Экспорт
	Результат = АдаптерВанессаАДД.Разобрать(ИмяОбработки);
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры

Процедура ВыполнитьТестВспомогательный(ИмяОбработки) Экспорт
	Результат = АдаптерВанессаАДД.ВыполнитьТест(ИмяОбработки);
	УведомленияПользователям.СобытиеЛога(Результат);
КонецПроцедуры

#КонецОбласти


